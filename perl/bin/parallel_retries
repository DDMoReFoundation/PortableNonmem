#!perl.exe
use FindBin qw($Bin);
use lib "$Bin/../site/lib/PsN_4_3_1";
# the above was entered by psnGlobalToLocalPaths

# Perl includes #
use Config;
use strict;
use Getopt::Long;
# External modules #
use Math::Random;
# PsN includes #
use PsN;
use tool::modelfit;
use model;
use common_options;
use ui;
use Cwd;
use File::Copy qw/cp mv/;

my $cmd_line = $0 . " " . join( " ", @ARGV );

my %options;

my %required_options = ();
my %optional_options = ("prepend_options_to_lst!"=> undef,
						"prepend_model_to_lst!"=> undef,
						"model_dir_name!" => undef,
						'rawres_input:s' => undef,
						'offset_rawres:i' => undef,
						'samples:i' => undef,
						'in_filter:s' => undef,
						"tail_output!" => undef,
						"wintail_exe:s" => undef,
						"wintail_command:s" => undef
	);

my $res = GetOptions( \%options,
		      @common_options::get_opt_strings,
		      keys(%optional_options) );

exit unless $res;


common_options::setup( \%options, 'execute' ); #calls set_globals etc, initiates random sequence


my %help_text;

$help_text{Pre_help_message} = <<'EOF';
  <h3 class="heading1">parallel_retries</h3>

    Running a modelfile with multiple sets of tweaked initial estimates,
	either randomly perturbed or from rawres_input.
    The program takes the same input as execute, see execute -h, except
    that either -min_retries or -rawres_input is required and 
	only a single input model can be used.
EOF

$help_text{-rawres_input} = <<'EOF';
      -rawres_input=filename

      An alternative way to estimate with different initial estimates. 
	  Instead of using initial estimates from a random perturbation,
      take parameter initial values from a raw_results-like file.
      The raw results file must contain at least as many 
      samples as the input -samples to parallel_retries, the labels for  THETA/OMEGA/SIGMA 
      in the file must match the labels in the simulation model given as input 
      to parallel_retries, the theta columns must be directly followed by the omega columns 
      which must be directly followed by the sigma columns, and the first column 
      must have header model just as a bootstrap raw_results file. Note that is is 
      possible to generate a file with initial parameter estimates outside of PsN, 
      as long as the file follows the format rules.
EOF
$help_text{-offset_rawres} = <<'EOF';
      -offset_rawres=N

      Only relevant in combination with rawres_input. Default 1.
      The number of result lines to skip in the input raw results file
      before starting to read final parameter estimates. In a regular
      bootstrap raw_results file the first line of estimates refers to the
      input model with the full dataset, so therefore the default offset is 1.
EOF

$help_text{-samples} = <<'EOF';
      -samples=N

      Only relevant in combination with rawres_input. 
      The number of parameter sets to use from the the input raw results file.
	  Default is to use all sets after skipping the first 'offset_rawres' sets.
EOF

$help_text{-in_filter} = <<'EOF';
      -in_filter=comma-separated list of conditions

      Only relevant in combination with rawres_input. Default not used.
      The parameter estimates lines in the file can be filtered on values 
      in the different columns. When specifying which column(s) the filtering 
      should be based on, the exact column name must be used, 
      e.g. minimization_successful. Filtering can only be based on columns 
      with numeric values. The allowed relations are .gt. (greater than), 
      .lt. (less than) and .eq. (equal to). Conditions are separated with commas. 
      If the remaining number of lines after filtering is smaller than -samples, 
      parallel_retries will stop with an error message. Then the user must either change the 
      filtering rules or change -samples. If the user has created a file with 
      parameter estimates outside of PsN, filtering can be done on any numeric 
      column in that file. Do not set column headers containing .eq. or .lt. 
      or .gt.in the user-generated file as this would interfere with the in_filter 
      option syntax.
    
      Example: -in_filter=minimization_successful.eq.1,significant_digits.gt.3.5

EOF



common_options::online_help('parallel_retries', \%options,\%help_text, \%required_options, \%optional_options);

unless (defined $options{'degree'}){
	my %hash = %{common_options::restore_options(@common_options::tool_options)};
	if (defined $hash{'degree'}){
		$options{'degree'} = $hash{'degree'};
	}else{
		$options{'degree'} = 0.1;
	}
}

## Set the automatic renaming of modelfit directory
if ( !$options{'directory'} && $options{'model_dir_name'} ) {
 
    my $dirnamebase = $ARGV[0].".dir";
    my $i = 1;
    while(-e $dirnamebase.".$i") {$i++};
    my $dirname = $dirnamebase.".$i";
    $options{'directory'} = $dirname;
}


my @outputfiles;
my $fake;
if( $options{'outputfile'} ){
  @outputfiles = split( /,/, $options{'outputfile'} );
}


my $models_array;

my $eval_string = common_options::model_parameters(\%options);

if( scalar @ARGV > 1 ) {
  die "When using parallel_retries, no "
      ."more than one model at a time may be run\n";
}
if( (not (defined $options{'min_retries'}) or ($options{'min_retries'}<1)) and (not defined $options{'rawres_input'}) ) {
  die "No point using parallel_retries unless -min_retries>0 or -rawres_input is defined\n";
}

if( ((defined $options{'min_retries'}) and ($options{'min_retries'}>0)) and (defined $options{'rawres_input'}) ) {
  die "Cannot use both -min_retries>0 and -rawres_input\n";
}


foreach my $model_name ( @ARGV ){  
  my $outputfile = shift @outputfiles;
  my $model;
  
  $model = model -> new ( eval( $eval_string ),
			  outputfile                  => $outputfile,
			  filename                    => $model_name,
			  ignore_missing_output_files => 1 );
  
  if( $options{'nonparametric_etas'} or
      $options{'nonparametric_marginals'} ) {
    $model -> add_nonparametric_code;
  }
  
  if( $options{'shrinkage'} ) {
    $model -> shrinkage_stats( enabled => 1 );
  }
  
  push( @{$models_array}, $model );
}

if($options{'tail_output'} ) {
  if($Config{osname} ne 'MSWin32'){
    print "Warning: option -tail_output only works on Windows.\n";
    $options{'tail_output'}=0;
  }
  unless (defined $options{'wintail_exe'} ) {
    print "Warning: option -wintail_exe is not set, -tail_output will not work\n";
    $options{'tail_output'}=0;
  }
  unless (defined $options{'wintail_command'} ) {
    print "Warning: option -wintail_command is not set, -tail_output will not work\n";
    $options{'tail_output'}=0;
  }
}

my $modelfit;


my $return_dir = getcwd();
my $main_directory = $options{'directory'};
my $dirt;
if (defined $main_directory){
  ($main_directory,$dirt) =OSspecific::absolute_path(undef,$main_directory);
}else{
  $main_directory= OSspecific::unique_path( 'parallel_retries_dir' ,$return_dir);
}

my $sampled_params_arr;
my $samples;
if (defined $options{'rawres_input'}){
	my $offset=1;
	$offset= $options{'offset_rawres'} if (defined $options{'offset_rawres'});
	my @in_filter=();
	if ( defined $options{'in_filter'} ){
		#split string, assume comma separated
		foreach my $filt (split(/,/,$options{'in_filter'})){
			if ($filt =~ /.\.(gt|lt|eq)\.\d+\.?\d*/){
				push(@in_filter,$filt);
			}else{
				die "Input filter $filt does not fulfill the format rules.\n";
			}
		}
		if (scalar(@in_filter)<1){
			die "Error: Option in_filter used, but list of conditions could not be parsed.\n";
		}	
	}

	$sampled_params_arr = $models_array -> [0]->get_rawres_params(filename => $options{'rawres_input'},
																  filter => \@in_filter,
																  offset => $offset);

	if (defined $sampled_params_arr) {
		if (defined $options{'samples'}){
			$samples=$options{'samples'};
			croak("Cannot have zero samples") if ($samples==0);
			unless (scalar(@{$sampled_params_arr}) >= ($samples)) {
				if (defined $options{'in_filter'}) {
					croak("Too few sets (lines) of parameter values in\n".
						  "rawres file after filtering. Have ".scalar(@{$sampled_params_arr}).
						  " but need at least $samples\n");
				} else {
					croak("Too few sets (lines) of parameter values in\n".
						  "rawres file. Have ".scalar(@{$sampled_params_arr}).
						  " but need at least ".
						  ($samples + $options{'offset_rawres'}).".\n");
				}
			}

		}else{
			$samples = scalar(@{$sampled_params_arr});
			if ($samples < 1){
				croak("No lines left in rawres (after filtering)");
			}
		}
	} else {
		croak("get_rawres_params returned undef");
	}


}
mkdir( $main_directory) unless ( -d  $main_directory);
chdir($main_directory);
my $main_model = $models_array -> [0] -> copy( filename    => $models_array -> [0]->filename(),
											   copy_datafile   => 1,
											   copy_output => 0,
											   directory => $main_directory);

my $mod_array;
push(@{$mod_array},$main_model);
if (defined $options{'rawres_input'}){
	foreach (my $retry=1;$retry<=$samples;$retry++){
		my $filestem = $models_array -> [0]->filename();
		$filestem =~ s/\.mod//;
		my $retry_mod = $main_model -> copy( filename    => $filestem.'_init'.$retry.'.mod',
											 copy_datafile   => 0,
											 copy_output => 0,
											 write_copy => 0,
											 directory => $main_directory);
		$retry_mod -> update_inits(from_hash => $sampled_params_arr->[$retry-1]); 
		$retry_mod -> _write();
		push(@{$mod_array},$retry_mod);
	}

}else{
	foreach (my $retry=1;$retry<=$options{'min_retries'};$retry++){
		my $filestem = $models_array -> [0]->filename();
		$filestem =~ s/\.mod//;
		my $retry_mod = $main_model -> copy( filename    => $filestem.'_retry'.$retry.'.mod',
											 copy_datafile   => 0,
											 copy_output => 0,
											 write_copy => 0,
											 directory => $main_directory);
		my @problems = @{$retry_mod -> problems};
		foreach my $prob ( @problems ) {
			$prob -> set_random_inits ( degree => $options{'degree'} );
		}
		$retry_mod -> datafiles(new_names => $main_model->datafiles(absolute_path => 1)); #use local data file
		$retry_mod -> _write();
		push(@{$mod_array},$retry_mod);
	}
}
#basedirect $main_directory
$modelfit = tool::modelfit -> 
    new ( eval( $common_options::parameters ),
	  prepend_model_to_lst => $options{'prepend_model_to_lst'},
	  prepend_model_file_name => 1,
	  prepend_options_to_lst => $options{'prepend_options_to_lst'},
	  tail_output => $options{'tail_output'},
	  wintail_exe => $options{'wintail_exe'},
	  wintail_command => $options{'wintail_command'},
	  directory => 'modelfit_dir1',
	  min_retries => 0,
	  retries =>0,
	  copy_data =>0,
	  models => $mod_array );  



$modelfit-> print_options (cmd_line => $cmd_line,
			   directory => $main_directory,
			   toolname => 'parallel_retries',
			   local_options => [keys %optional_options],
			   common_options => \@common_options::tool_options);


$modelfit -> run;

mv('modelfit_dir1/raw_results.csv','raw_results.csv');


ui -> print( category => 'parallel_retries',
	     message => "\nparallel_retries done\n" );
