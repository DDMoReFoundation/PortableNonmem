#!perl.exe
use FindBin qw($Bin);
use lib "$Bin/../site/lib/PsN_4_3_1";
# the above was entered by psnGlobalToLocalPaths

use PsN;
use model;
use tool::cdd;
use strict;
use Getopt::Long;
use common_options;

my $cmd_line = $0 . " " . join( " ", @ARGV );

## Configure the command line parsing
Getopt::Long::config("auto_abbrev");

my %options;
## Declare the options

my %required_options = ( 'case_column:s'        => 'column_name|column_number');
my %optional_options = ( "bins:i"               => '',
			 "xv!"                  => '',
			 "selection_method:s"   => '\'random\'|\'consecutive\'',
			 "outside_n_sd_check:f" => '' );

my $res = GetOptions( \%options,
		      @common_options::get_opt_strings,
		      keys(%required_options),
		      keys(%optional_options) );
exit unless $res;

common_options::setup( \%options, 'cdd' ); #calls set_globals etc, initiates random sequence


my %help_text;

$help_text{Pre_help_message} = <<'EOF';
EOF

$help_text{Description} = <<'EOF';
<h3 class=heading1>Description</h3>

      The Case Deletion Diagnostics tool is run using the command from the command line
      with a few mandatory arguments. CDD is run as a diagnostic after
      a model is regarded finished or at least mature enough to run
      validation tool on. You need to specify the NONMEM modelfile
      with a model that have successful termination. You also have to
      specify the number or name of the datafile column on which to
      select for deletion. You do so with the case_column option.
EOF

$help_text{Examples} = <<'EOF';
<h3 class=heading1>Examples</h3>
      
      cdd -model=run89.mod -case_column=10
      
      This will perform a Case Deletion Diagnostic on the model
      specified in run89.mod based on the factors in column ten. If,
      for example, column ten holds the ids of the seven centers
      included in the study, this command will create seven copies of
      the dataset, each with individuals included in one specific
      center deleted. Say that the centers are numbered 1 to 7. Then
      dataset 1 will have individuals from center 1 excluded, dataset
      2 individuals from center 2 and so on.
EOF

$help_text{Options} = <<'EOF';  
      <h3 class=heading1>Options</h3>

      The options are given here in their long form. Any option may be
      abbreviated to any nonconflicting prefix. The -threads option
      may be abbreviated to -t(or even -thr).

      The following options are valid:
EOF

$help_text{-h} = <<'EOF';
      <p class="option">-h | -?</p>
      
      With -h or -? cdd will print a list of options and exit.
EOF
      
$help_text{-help} = <<'EOF';      
      <p class="option">-help</p>
      
      With -help cdd will print this, longer, help message.
EOF

$help_text{-bins} = <<'EOF';
      <p class="option">-bins=$number</p>

      Sets the number of databins, or cdd datasets, to use. If the
      number of unique values, or factors, in the based_on column is
      higher than the number of bins then one or more factors will be
      deleted in each cdd dataset. Specifying $number as higher than
      the number of factors will have no effect. The bin number is
      then set to the number of factors.
      Default value = Number of unique values in the based_on column.
EOF

$help_text{-selection_method} = <<'EOF';
      <p class="option">-selection_method='random' or 'consecutive'</p>

      Specifies whether the factors selected for exclusion should be
      drawn randomly or consecutively from the datafile.
      Default value = 'consecutive'
EOF

$help_text{-case_column} = <<'EOF';
      <p class="option">-case_column=column_name|column_number</p>

      This flag is mandatory. Through this flag you set the column on
      which the case-deletion is done. You can either use the name of
      the column as specified in the $INPUT record in the model file
      or you can use the column number. Numbering starts with 1.

EOF

$help_text{-outside_n_sd_check} = <<'EOF';
      <p class="option">-outside_n_sd_check='number'</p>

    Mark the runs with CS-CR outside this many standard deviations of the PCA.    
EOF

$help_text{-xv} = <<'EOF';
      <p class="option">-xv|-no-xv</p>

      Turns the cross-validation feature on (-xv) or off (-no-xv). The
      default behaviour is to run the cross-validation step.
EOF


$help_text{Post_help_message} = <<'EOF';
      Also see 'psn_options -h' for a description of common options.
EOF

common_options::online_help( 'cdd', \%options, \%help_text, \%required_options, \%optional_options);

## Check that we do have a model file
if ( scalar(@ARGV) < 1 ) {
  print "A model file must be specified. Use 'cdd -h' for help.\n";
  exit;
}

if( scalar(@ARGV) > 1 ){
  print "CDD can only handle one modelfile. Use 'cdd -h' for help.\n";die;
  exit;
}

unless ( defined $options{'case_column'} ){
  print "case_column must be given\n" ;
  exit;
}

if ( defined $options{'dummy_data'} ){
  #never allow dummy_data with cdd
  $options{'dummy_data'} = 0;
}

my $eval_string = common_options::model_parameters(\%options);

my $model = model -> new ( eval( $eval_string ),
						   filename                    => $ARGV[0],
						   ignore_missing_output_files => 1 );

if ( not $options{'case_column'} =~ /^\d/ ) {
    # STRING
    my ( $junk, $column_position ) = $model ->
	_get_option_val_pos( name            => $options{'case_column'},
			     record_name     => 'input',
			     problem_numbers => [1] );
    # We assume that there is no duplicate column names
    unless (defined $column_position->[0][0]){
	print "Error finding column ".$options{'case_column'}. " in \$INPUT of model\n";
	exit;
    }
    $options{'case_column'}= $column_position->[0][0];
} 


if( $options{'shrinkage'} ) {
  $model -> shrinkage_stats( enabled => 1 );
}

## Create new Cdd object:
my $cdd = tool::cdd -> 
    new ( eval( $common_options::parameters ),
	  models	     => [ $model ],
	  bins		     => $options{'bins'},
	  selection_method   => $options{'selection_method'},
	  case_column       => $options{'case_column'},
	  outside_n_sd_check => $options{'outside_n_sd_check'},
	  cross_validate     => $options{'xv'} );


$cdd-> print_options (cmd_line => $cmd_line,
		     toolname => 'CDD',
		     local_options => [keys %optional_options,'cross_validate'],
		     common_options => \@common_options::tool_options);

$cdd -> run;
$cdd -> prepare_results();
$cdd -> print_results;

ui -> print( category => 'cdd',
	     message => "cdd done" );
